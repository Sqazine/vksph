#version 450 core

#define WORK_GROUP_SIZE 128
#define PARTICLE_NUM 20000
#define PI 3.1415927410125732421875
#define PARTICLE_RADIUS 0.005f
#define PARTICLE_RESTING_DENSITY 1000
#define PARTICLE_MASS 0.02 //m=pv
#define SMOOTHING_LENGTH (4*PARTICLE_RADIUS)
#define PARTICLE_STIFFNESS 2000

layout(local_size_x=WORK_GROUP_SIZE) in;

layout(std430,binding=0) buffer positionBuffer
{
    vec2 position[];
};

layout(std430,binding=1) buffer velocityBuffer
{
    vec2 velocity[];
};

layout(std430,binding=2) buffer forceBuffer
{
    vec2 force[];
};

layout(std430,binding=3) buffer densityBuffer
{
    float density[];
};

layout(std430,binding=4) buffer pressureBuffer
{
    float pressure[];
};

void main()
{
    uint i=gl_GlobalInvocationID.x;

    float densitySum=0.0f;
    for(int j=0;j<PARTICLE_NUM;++j)
    {
        vec2 delta=position[i]-position[j];
        float r=length(delta);
        if(r<SMOOTHING_LENGTH)
            densitySum+=PARTICLE_MASS*315.0f*pow(SMOOTHING_LENGTH*SMOOTHING_LENGTH-r*r,3)/(64.0f*PI*pow(SMOOTHING_LENGTH,9));
    }
    density[i]=densitySum;
    pressure[i]=max(PARTICLE_STIFFNESS*(densitySum-PARTICLE_RESTING_DENSITY),0.0f);
}

